// A. Program
S 			→ STATEMENT_LIST

// B. Statement
STATEMENT_LIST 		→ STATEMENT STATEMENT_LIST | ε
STATEMENT 		→ DECLARATION ';' | ASSIGNMENT ';' | EXPRESSION ';'

// C. Declaration
DECLARATION 		→ DATATYPE INIT_DECLARATOR_LIST
INIT_DECLARATOR_LIST 	→ INIT_DECLARATOR | INIT_DECLARATOR ',' INIT_DECLARATOR_LIST
INIT_DECLARATOR 	→ IDENTIFIER | IDENTIFIER '=' EXPRESSION
DATATYPE 		→ 'char' | 'int'

// D. Assignment
ASSIGNMENT 		→ IDENTIFIER ASSIGN_OP EXPRESSION
ASSIGN_OP 		→ '=' | '+=' | '-=' | '*=' | '/='

// E. Expression
EXPRESSION 		→ TERM EXPRESSION_PRIME
EXPRESSION_PRIME 	→ '+' TERM EXPRESSION_PRIME | '-' TERM EXPRESSION_PRIME | ε
TERM 			→ FACTOR TERM_PRIME
TERM_PRIME 		→ '*' FACTOR TERM_PRIME | '/' FACTOR TERM_PRIME | ε

// F. Factor
FACTOR 			→ PRIMARY FACTOR_TAIL
PRIMARY 		→ IDENTIFIER | INT_LITERAL | CHAR_LITERAL | '(' EXPRESSION ')' | UNARY_OP FACTOR
FACTOR_TAIL 		→ POSTFIX_OP FACTOR_TAIL | ε

// G. Unary / Postfix Operators
UNARY_OP 		→ '+' | '-' | '++' | '--'
POSTFIX_OP 		→ '++' | '--'

// H. Identifiers, Integers, Characters
IDENTIFIER → L (L | D | U)*
L = a | b | c | ... | z | A | B | C | ... | Z
D = 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
U = _

INT_LITERAL → ('-' | ε) (0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9)+

CHAR_LITERAL → '\'' CHARACTER '\''
CHARACTER → NON_ESCAPE_CHAR | ESCAPE_SEQUENCE
NON_ESCAPE_CHAR → (a-z | A-Z | 0-9 | space | symbol_except_\'_and_\\)
ESCAPE_SEQUENCE → '\\' ( 'n' | 't' | 'r' | '\'' | '\"' | '\\' | '0' )
